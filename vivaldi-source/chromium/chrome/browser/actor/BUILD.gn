# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")

assert(enable_glic)

source_set("actor") {
  friend = [
    ":unit_tests",
    ":browser_tests",
  ]
  public = [ "actor_coordinator.h" ]
  sources = [
    "actor_coordinator.cc",
    "actor_features.cc",
    "actor_features.h",
    "site_policy.cc",
    "site_policy.h",
    "tools/navigate_tool.cc",
    "tools/navigate_tool.h",
    "tools/page_tool.cc",
    "tools/page_tool.h",
    "tools/tool.h",
    "tools/tool_callbacks.cc",
    "tools/tool_callbacks.h",
    "tools/tool_controller.cc",
    "tools/tool_controller.h",
    "tools/tool_invocation.cc",
    "tools/tool_invocation.h",
  ]
  public_deps = [
    "//base",
    "//components/optimization_guide/proto:optimization_guide_proto",
  ]
  deps = [
    "//chrome/browser/safe_browsing",
    "//chrome/browser/ui/tabs:tabs_public",
    "//chrome/common",
    "//components/safe_browsing:buildflags",
    "//content/public/browser",
    "//net",
    "//url",
  ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "actor_test_util.cc",
    "actor_test_util.h",
  ]

  public_deps =
      [ "//components/optimization_guide/proto:optimization_guide_proto" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "actor_coordinator_unittest.cc",
    "site_policy_unittest.cc",
  ]
  deps = [
    ":actor",
    ":test_support",
    "//base/test:test_support",
    "//chrome/test:test_support",
    "//testing/gtest",
  ]
}

source_set("browser_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  sources = [
    "site_policy_browsertest.cc",
    "tools/tools_browsertest.cc",
  ]
  deps = [
    ":actor",
    ":test_support",
    "//base/test:test_support",
    "//chrome/browser/safe_browsing",
    "//chrome/browser/ui:ui",
    "//chrome/test:test_support",
    "//chrome/test:test_support_ui",
    "//components/safe_browsing:buildflags",
    "//components/safe_browsing/core/common:features",
    "//content/test:test_support",
    "//testing/gtest",
  ]
}
