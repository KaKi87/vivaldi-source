# Copyright (c) 2015-2017 Vivaldi Technologies AS. All rights reserved

source_set("invalidations") {
  sources = [
    "invalidations/invalidation_service_stomp_client.cc",
    "invalidations/invalidation_service_stomp_client.h",
    "invalidations/stomp_constants.h",
    "invalidations/stomp_frame_builder.cc",
    "invalidations/stomp_frame_builder.h",
    "invalidations/vivaldi_sync_invalidations_service.cc",
    "invalidations/vivaldi_sync_invalidations_service.h",
  ]

  deps = [
    "//components/invalidation/impl",
    "//vivaldi/vivaldi_account"
  ]

  if(!is_ios) {
    sources += [
      "invalidations/vivaldi_sync_invalidations_service_factory.cc",
      "invalidations/vivaldi_sync_invalidations_service_factory.h",
    ]
    deps += [
      "//chrome/common:buildflags",
      "//chrome/browser:browser_process",
      "//chrome/browser/profiles:profile",
      "//components/keyed_service/content",
      "//components/prefs",
      "//content/public/common",
      "//vivaldi/prefs:prefs_strings",
    ]
  }
}

static_library("factory") {
  if (!is_ios) {
    sources = [
      "file_sync/file_store_factory.cc",
      "file_sync/file_store_factory.h",
      "note_sync_service_factory.cc",
      "note_sync_service_factory.h",
    ]
    deps = [
      "//components/keyed_service/core",
      "//components/keyed_service/content",
      "//vivaldi/sync/notes:sync_notes",
    ]

    if (is_android) {
      sources += [
        "file_sync/file_store_android.cc",
        "file_sync/file_store_android.h",
      ]

      deps += [
        "//chrome/android:chrome_jni_headers",
      ]
    }
  }
}

source_set("browser") {
  sources = [
    "vivaldi_device_info_utils.cc",
    "vivaldi_device_info_utils.h",
    "vivaldi_sync_auth_manager.cc",
    "vivaldi_sync_auth_manager.h",
    "vivaldi_sync_ui_helpers.cc",
    "vivaldi_sync_ui_helpers.h",
    "vivaldi_sync_service_impl.cc",
    "vivaldi_sync_service_impl.h",
  ]
  deps = [
    ":factory",
    "//components/content_settings/core/browser",
    "//components/network_time",
    "//components/pref_registry",
    "//components/signin/core/browser",
    "//components/sync/protocol",
    "//components/translate/content/common",
    "//skia",
    "//third_party/icu",
    "//third_party/libaddressinput",
    "//vivaldi/components/notes:browser",
    "//vivaldi/prefs:prefs_files_generator",
  ]
  public_deps = [
    "//components/sync/protocol",
  ]
  allow_circular_includes_from = [
    "//vivaldi/components/notes:browser",
  ]

  if (is_android) {
    deps += [
      "//chrome/android:jni_headers",
    ]

    sources += [
      "//vivaldi/sync/vivaldi_sync_service_android.cc",
      "//vivaldi/sync/vivaldi_sync_service_android.h",
    ]
  }

  if (!is_android && !is_ios) {
    public_deps += [
      "//vivaldi/extensions",
      "//vivaldi/extensions/api:apis",
    ]
    allow_circular_includes_from += [
      "//vivaldi/extensions/api:apis",
    ]
  }

  if(!is_ios) {
    sources += [
    ]
    deps += [
      ":invalidations",
      "//chrome/browser/ui",
      "//chrome/common:buildflags",
      "//chrome/common:channel_info",
      "//components/keyed_service/content",
      "//extensions/buildflags",
      "//vivaldi/sync/notes:sync_notes",
      "//vivaldi/vivaldi_account",
    ]
    public_deps += [
      "//content/public/common",
    ]
  } else {
    deps += [
      "//components/keyed_service/ios",
      "//vivaldi/ios/vivaldi_account",
      "//vivaldi/ios/notes",
      "//vivaldi/ios/sync:invalidations",
    ]
  }
}
